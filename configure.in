AC_PREREQ(2.59)
AC_INIT([libexif CLI interface],[0.6.11],[libexif-devel@lists.sourceforge.net],[exif])
AC_CONFIG_SRCDIR([exif/main.c])
AC_CONFIG_HEADERS([config.h])
AM_INIT_AUTOMAKE([gnu 1.8 dist-bzip2])
GP_CONFIG_INIT
GP_CONFIG_MSG([Source code location],[${srcdir}])

AC_PROG_CC
AC_C_CONST
AM_PROG_LIBTOOL
AM_CFLAGS="$CFLAGS"
GP_CONFIG_MSG([Compiler],[${CC}])

AC_PATH_PROG(PKG_CONFIG,pkg-config)

dnl ---------------------------------------------------------------------------
dnl i18n support
dnl ---------------------------------------------------------------------------
GP_GETTEXT_HACK([${PACKAGE}],[Lutz MÃ¼ller and others])
ALL_LINGUAS="de es fr"
AM_GNU_GETTEXT_VERSION([0.14.1])
AM_GNU_GETTEXT([external])
GP_GETTEXT_FLAGS

dnl We cannot use AC_DEFINE_UNQUOTED() for these definitions, as
dnl we require make to do insert the proper $(datadir) value
localedir="\$(datadir)/locale"
AC_SUBST(localedir)
AM_CFLAGS="$AM_CFLAGS -DEXIF_LOCALEDIR=\\\"${localedir}\\\""


dnl ---------------------------------------------------------------------------
dnl libraries needed
dnl ---------------------------------------------------------------------------
PKG_CHECK_MODULES([LIBEXIF], [libexif >= 0.6.11])
AC_SUBST([LIBEXIF_LIBS])
AC_SUBST([LIBEXIF_CFLAGS])


dnl ---------------------------------------------------------------------------
dnl locale.h: locale.h provides setlocale. It seems that some systems don't
dnl           have it (at least if configured --without-nls).
dnl ---------------------------------------------------------------------------
AC_CHECK_HEADERS([locale.h])
AC_CHECK_HEADER([iconv.h], [
	AC_DEFINE(HAVE_ICONV,1,[whether iconv is available])])


dnl ---------------------------------------------------------------------------
dnl popt.h: Simplifies handling of command-line options enormously.
dnl ---------------------------------------------------------------------------
popt_msg="no (Note that popt is mandatory, so exif won't compile)"
try_popt=true
have_popt=false
AC_ARG_WITH(popt, [  --without-popt            Don't use popt.h],
	if test "x${withval}" = "xno"; then
		try_popt=false
		popt_msg="no (not requested)"
	fi
)
popt_prefix=$ac_default_prefix
AC_ARG_WITH(popt-prefix, [  --with-popt-prefix=PREFIX  Location of popt], [
	popt_prefix="$withval"])
if $try_popt; then
	CPPFLAGS_save="$CPPFLAGS"
	CPPFLAGS="-I$popt_prefix/include $CPPFLAGS"
	AC_CHECK_HEADER(popt.h, [
			AC_CHECK_LIB(popt, poptResetContext, [
			have_popt=true
			popt_msg=yes
			AC_DEFINE(HAVE_POPT,1,[whether we have popt])
			POPT_LIBS="-lpopt"
			POPT_CFLAGS=$CPPFLAGS
		], [
			LDFLAGS_save="$LDFLAGS"
			LDFLAGS="-L$popt_prefix/lib"
			AC_CHECK_LIB(popt, poptStuffArgs, [
				have_popt=true
				popt_msg="yes (in '$popt_prefix')"
				AC_DEFINE(HAVE_POPT,1,[whether we have popt])
				POPT_LIBS="-L$popt_prefix/lib -lpopt"
				POPT_CFLAGS=$CPPFLAGS
		], [popt_pmsg="no (couldn't link)"])
		LDFLAGS=$LDFLAGS_save
		])
	])
	CPPFLAGS="$CPPFLAGS_save"
fi
AM_CONDITIONAL([HAVE_POPT], [$have_popt])
AC_SUBST([POPT_LIBS])
AC_SUBST([POPT_CFLAGS])


dnl ---------------------------------------------------------------------------
dnl Warnings: If we have GCC, be paranoid.
dnl ---------------------------------------------------------------------------
if test "x$GCC" = "xyes"; then
    AM_CFLAGS="$AM_CFLAGS -Wall -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith"
    AM_LDFLAGS="$AM_LDFLAGS -g -Wall"
fi

AC_SUBST([AM_CFLAGS])
AC_SUBST([AM_LDFLAGS])

dnl ---------------------------------------------------------------------------
dnl Output files
dnl ---------------------------------------------------------------------------
AC_CONFIG_FILES([
  po/Makefile.in
  Makefile
  m4/Makefile
  exif.spec
  libjpeg/Makefile
  exif/Makefile
])
AC_OUTPUT

GP_CONFIG_OUTPUT
