AC_PREREQ(2.59)
AC_INIT([libexif command line interface],[0.6.13],[libexif-devel@lists.sourceforge.net],[exif])
AC_CONFIG_SRCDIR([exif/main.c])
AC_CONFIG_HEADERS([config.h])
AM_INIT_AUTOMAKE([gnu 1.8 dist-bzip2])
GP_CONFIG_INIT
GP_CONFIG_MSG([Source code location],[${srcdir}])

AC_PROG_CC
AC_C_CONST
AM_PROG_LIBTOOL
AM_CFLAGS="$CFLAGS"
GP_CONFIG_MSG([Compiler],[${CC}])

GP_PKG_CONFIG

# ---------------------------------------------------------------------------
# i18n support
# ---------------------------------------------------------------------------
GP_GETTEXT_HACK([${PACKAGE}],[Lutz MÃ¼ller and others])
ALL_LINGUAS="de es fr"
AM_GNU_GETTEXT_VERSION([0.14.1])
AM_GNU_GETTEXT([external])
AM_PO_SUBDIRS()
AM_ICONV()
GP_GETTEXT_FLAGS()

# We cannot use [AC_DEFINE_UNQUOTED()] for these definitions, as
# we require make to do insert the proper $(datadir) value
AC_SUBST([localedir],["\$(datadir)/locale"])
AM_CFLAGS="$AM_CFLAGS -DLOCALEDIR=\\\"${localedir}\\\""


# ---------------------------------------------------------------------------
# libraries needed
# ---------------------------------------------------------------------------
AC_ARG_VAR([LIBEXIF_LIBS],[libs to add for linking against libexif])
AC_ARG_VAR([LIBEXIF_CFLAGS],[CFLAGS for compiling with libexif])
AC_MSG_CHECKING([which libexif to use])
if test "x$LIBEXIF_LIBS" = "x" && test "x$LIBEXIF_CFLAGS" = "x"; then
	AC_MSG_RESULT([autodetect])
	PKG_CHECK_MODULES([LIBEXIF], [libexif >= 0.6.11], [], [AC_MSG_ERROR([
* Fatal: ${PACKAGE_NAME} requires libexif to build.
])])
elif test "x$LIBEXIF_LIBS" != "x" && test "x$LIBEXIF_CFLAGS" != "x"; then
	AC_MSG_RESULT([user-defined])
else
	AC_MSG_ERROR([
* Either set LIBEXIF_LIBS *and* LIBEXIF_CFLAGS or none at all
* when calling configure for the ${PACKAGE_NAME}.
])
fi
AC_SUBST([LIBEXIF_LIBS])
AC_SUBST([LIBEXIF_CFLAGS])
GP_CONFIG_MSG([libexif library],[${LIBEXIF_LIBS}])
GP_CONFIG_MSG([libexif cflags],[${LIBEXIF_CFLAGS}])


# ---------------------------------------------------------------------------
# locale.h: locale.h provides setlocale. It seems that some systems don't
#           have it (at least if configured --without-nls).
# ---------------------------------------------------------------------------
AC_CHECK_HEADERS([locale.h])
AC_CHECK_HEADER([iconv.h], [
	AC_DEFINE(HAVE_ICONV,1,[whether iconv is available])])


# ---------------------------------------------------------------------------
# popt.h: Simplifies handling of command-line options enormously.
# ---------------------------------------------------------------------------
GP_CHECK_POPT([mandatory])dnl


# ---------------------------------------------------------------------------
# Warnings: If we have GCC, be paranoid.
# ---------------------------------------------------------------------------
if test "x$GCC" = "xyes"; then
    AM_CFLAGS="$AM_CFLAGS -Wall -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith"
    AM_LDFLAGS="$AM_LDFLAGS -g -Wall"
fi

AC_SUBST([AM_CFLAGS])dnl
AC_SUBST([AM_LDFLAGS])dnl


# ---------------------------------------------------------------------------
# Output files
# ---------------------------------------------------------------------------
AC_CONFIG_FILES([
  po/Makefile.in
  Makefile
  m4/Makefile
  exif.spec
  libjpeg/Makefile
  exif/Makefile
])
AC_OUTPUT()dnl

GP_CONFIG_OUTPUT()dnl
